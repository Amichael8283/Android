apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename)
        }
    } else {
        print(filename + " does not exist!")
    }
}

def keystorePropertiesFile = rootProject.file("config/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        applicationId "org.michaelbel.moviemade"
        /*defaultPublishConfig 'debug'*/

        versionCode 69
        versionName "1.3.2"
        project.ext.set("archivesBaseName", "moviemade-v" + versionName)
        buildConfigField "long", "VERSION_DATE", System.currentTimeMillis() + "L"
        buildConfigField "String", "TMDB_API_KEY", "\"${getProperty("local.properties", "TMDB_API_KEY")}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            applicationIdSuffix ".debug"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    androidExtensions {
        experimental = true
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE'
        exclude 'LICENSE.txt'
        exclude 'NOTICE'
        exclude 'NOTICE.txt'

        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        lintConfig file('lint.xml')

        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false

        /*textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        ignoreWarnings false
        checkReleaseBuilds true
        htmlReport true
        xmlReport true
        lintConfig file("$projectDir/lint.xml")
        fatal 'StopShip'*/
        
        //disable 'MissingTranslation'
        //disable 'ExtraTranslation'
    }
}

dependencies {
    implementation project(':core')
    implementation project(':domain')

    implementation "android.arch.work:work-runtime:$work_version"
    implementation "android.arch.work:work-runtime-ktx:$work_version"

    implementation "com.google.android.play:core:$google_play_core_version"
    implementation "com.google.android.material:material:$google_material_version"

    implementation "androidx.cardview:cardview:$androidx_cardview_version"
    implementation "androidx.activity:activity:$androidx_activity_version"
    implementation "androidx.activity:activity-ktx:$androidx_activity_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.appcompat:appcompat-resources:$androidx_appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint_version"
    implementation "androidx.vectordrawable:vectordrawable:$androidx_vector_drawable_version"
    implementation "androidx.vectordrawable:vectordrawable-animated:$androidx_vector_drawable_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "com.tspoon.traceur:traceur:$traceur_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

    debugImplementation "com.github.ajitsing:sherlock:$sherlock_version"
    releaseImplementation "com.github.ajitsing:sherlock-no-op:$sherlock_version"

    debugImplementation "com.readystatesoftware.chuck:library:$chuck_version"
    releaseImplementation "com.readystatesoftware.chuck:library-no-op:$chuck_version"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"

    implementation 'com.beloo.widget:ChipsLayoutManager:0.3.7' // fixme remove
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:8.0.1' // fixme remove

    implementation 'com.alexvasilkov:events:1.0.0' // fixme remove
    implementation 'com.alexvasilkov:gesture-views:2.4.0' // fixme remove
    implementation 'com.alexvasilkov:android-commons:2.0.2' // fixme remove
}

apply plugin: 'com.google.gms.google-services'